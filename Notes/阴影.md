
# 阴影贴图
先从光源视角渲染一次获得深度阴影贴图
在从摄像机渲染一次，根据阴影贴图的深度和当前像素的深度对比是否应该生成阴影效果

``` glsl
float depth = (worldPos - LightPos).length;

float depthShadowMap = tex2D(ShadowMap, UVShadow);

if(depth > depthShadowMap)

{

return float4(0,0,0,1);

}

else

{

return tex2D(Texture, uv);

}
```

# PCF
采样取均值，PCF的滤波半径决定了阴影边缘的柔和程度，一般来说滤波半径越大则柔和，相反阴影边缘越锐利
```glsl

float shadow = 0.0;
vec2 texelSize = 1.0 / textureSize(shadowMap, 0);
for(int x = -1; x <= 1; ++x)
{
    for(int y = -1; y <= 1; ++y)
    {
        float pcfDepth = texture(shadowMap, projCoords.xy + vec2(x, y) * texelSize).r; 
        shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;        
    }    
}
shadow /= 9.0;

```

# PCSS
由PCF启发，软阴影的边缘用更大的滤波半径进行滤波，而对于那些不需要太过柔和阴影边缘的地方，则采用更小的滤波半径
![](https://cdn.jsdelivr.net/gh/ZeusYang/CDN-for-yangwc.com@1.1.41/blog/PCSS/9.jpg)


```glsl
float findBlocker( sampler2D shadowMap,  vec2 uv, float zReceiver ) {  
  const int radius = 40;  
  const vec2 texelSize = vec2(1.0/2048.0, 1.0/2048.0);  
  float cnt = 0.0, blockerDepth = 0.0;  
  int flag = 0;  
  for(int ns = 0;ns < BLOCKER_SEARCH_NUM_SAMPLES;++ns)  
  {  
      vec2 sampleCoord = (vec2(radius) * poissonDisk[ns]) * texelSize + uv;  
      float cloestDepth = unpack(texture2D(shadowMap, sampleCoord));  
      if(zReceiver - 0.002 > cloestDepth)  
      {  
        blockerDepth += cloestDepth;  
        cnt += 1.0;  
        flag = 1;  
      }  
  }  
  if(flag == 1)  
  {  
	  return blockerDepth / cnt;  
  }  
  return 1.0;  
}  
  
float PCF(sampler2D shadowMap, vec4 shadowCoord, float radius) {  
  const vec2 texelSize = vec2(1.0/2048.0, 1.0/2048.0);  
  float visibility = 0.0, cnt = 0.0;  
  for(int ns = 0;ns < PCF_NUM_SAMPLES;++ns)  
  {  
    vec2 sampleCoord = (vec2(radius) * poissonDisk[ns]) * texelSize + shadowCoord.xy;  
    float cloestDepth = unpack(texture2D(shadowMap, sampleCoord));  
    visibility += ((shadowCoord.z - 0.001) > cloestDepth ? 0.0 : 1.0);  
    cnt += 1.0;  
  }  
  return visibility/cnt;  
}  
  
float PCSS(sampler2D shadowMap, vec4 shadowCoord){  
  
  // STEP 1: avgblocker depth  
  float avgBlockerDepth = findBlocker(shadowMap, shadowCoord.xy, shadowCoord.z);  
  
  // STEP 2: penumbra size  
  const float lightWidth = 50.0;  
  float penumbraSize = max(shadowCoord.z-avgBlockerDepth,0.0)/avgBlockerDepth*lightWidth;  
  
  // STEP 3: filtering  
  return PCF(shadowMap, shadowCoord, penumbraSize);  
  //return 1.0;  
  
}
```
